#INCLUDE "Protheus.ch"
#INCLUDE "RWMake.ch"
#INCLUDE "CXInclude.ch"
#INCLUDE "CXSTRUCT.CH"

//ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
//±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
//±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
//±±³Programa  ³ CXFieldPut ³ Autor ³ Cirilo Rocha        ³ Data ³02/08/2011³±±
//±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
//±±³Descricao ³ Funcao customizada para simplificar o uso da fucao padrao  ³±±
//±±³          ³ FieldPut.                                                  ³±±
//±±³          ³                                                            ³±±
//±±³          ³ Ao invez de utilizar FieldPut(FieldPos(cCampo)) pode-se    ³±±
//±±³          ³ usar apenas U_CXFieldPut(cCampo)                           ³±±
//±±³          ³                                                            ³±±
//±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
//±±³   DATA   ³ Programador   ³ Manutencao efetuada                        ³±±
//±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
//±±³ 16/02/12 ³ Cirilo Rocha  ³ Ajuste para obter a tabela por parametro ou³±±
//±±³          ³               ³ com base no proprio campo visando simplifi-³±±
//±±³          ³               ³ car o uso desta funcao.                    ³±±
//±±³ 22/02/12 ³ Cirilo Rocha  ³ Acrescentado tratamento de erro para os ca-³±±
//±±³          ³               ³ sos do campo nao existir no dicionario.    ³±±
//±±³ 23/02/12 ³ Cirilo Rocha  ³ Feito tratamento para campos Memo virtuais ³±±
//±±³          ³               ³ (antigos)                                  ³±±
//±±³ 28/09/12 ³ Cirilo Rocha  ³ Otimizacao para gravar apenas quando o con-³±±
//±±³          ³               ³ teudo for diferente.                       ³±±
//±±³ 01/10/12 ³ Cirilo Rocha  ³ Tratamento para campos tipo Data em que o  ³±±
//±±³          ³               ³ novo conteudo foi passado na forma string  ³±±
//±±³ 08/10/12 ³ Cirilo Rocha  ³ Ajuste para gravacao de campos memo.       ³±±
//±±³ 08/10/13 ³ Cirilo Rocha  ³ Ajustado o retorno para considerar apenas  ³±±
//±±³          ³               ³ .T. ou .F. se alterou o conteudo do campo  ³±±
//±±³ 07/11/13 ³ Cirilo Rocha  ³ Correcao no tratamento dos campos Memo.    ³±±
//±±³          ³               ³ Alterado o padrao para obter o alias do a  ³±±
//±±³          ³               ³ partir do campo e nao da area atual        ³±±
//±±³ 22/10/15 ³ Cirilo Rocha  ³ Pequena otimizacao no fonte                ³±±
//±±³          ³               ³                                            ³±±
//±±³          ³               ³                                            ³±±
//±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
//±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
//ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
Static aPosCpos	:= {}

//-----------------------------------------------------------------------------
User Function CXFieldPut(cCampo,xConteudo,cAlias,lAliasCpo,lInclui)

	Local lAlterou	:= .F.
	Local lMemo		:= .F.
	Local aArea
	
	Local nPosCpo
	Local nPosIni
	Local nPosFim
	Local nPosArr
	Local nPosArr2
	Local cIni
	Local cCpoMemo
	Local xAtual
	Local nTamCpo
	Local cTipo
	Local lGrava
	
	Default lAliasCpo	:= .T.
	Default lInclui	:= .F.

	cCampo	:= U_CXRTrim(Upper(cCampo))

	If cAlias == NIL
		If lAliasCpo
			cAlias	:= U_CXTabela(cCampo)
		Else
			cAlias	:= Alias()
		EndIf
	EndIf
	
	nPosCpo 	:= (cAlias)->(FieldPos(cCampo))

	//O campo nao existe na tabela fisica, busca se e' um memo
	If nPosCpo == 0
		aArea		:= U_CXGetArea({'SX3'},.F.)
		SX3->(U_CXSetOrd(2)) // X3_CAMPO
	
		//Verifica se e' um Memo antigo (virtual)
		If SX3->(MsSeek(cCampo))
			If SX3->X3_CONTEXT == 'V' .And. ;
				SX3->X3_TIPO == 'M'
				
				cIni		:= RTrim(SX3->X3_RELACAO)
				nPosIni	:= At('->',cIni)+1
				nPosFim	:= At(',',Right(cIni,len(cIni)-nPosIni))
				If nPosIni > 0 .And. nPosFim > 4
					cCpoMemo	:= SubStr(cIni,nPosIni+1,nPosFim-1)
					dbSelectArea(cAlias)
					
					MSMM(U_CXFieldGet(cCpoMemo,cAlias),,,xConteudo,1,,,cAlias,cCpoMemo) //Grava observacao
					lAlterou	:= .T.
					lMemo		:= .T.
				Else
					ApMsgAlert('CXFieldPut-001: O campo '+cCampo+' não pode ser escrito. Informe ao setor de T.I.')
				EndIf
			EndIf
		Else
			ApMsgAlert('CXFieldPut-002: O campo '+cCampo+' não foi localizado no SX3. Informe ao setor de T.I.')
		EndIf

		U_CXRestArea(aArea)

		If !lMemo
			ApMsgAlert('CXFieldPut-003: O campo '+cCampo+' não foi localizado na tabela '+cAlias+'. Informe ao setor de T.I.')
		EndIf
	EndIf

	If !lMemo
		
		If lInclui
			lGrava	:= !Empty(xConteudo)
		EndIf
		
		//Compatibiliza os tipos e tamanhos de campos
		If !lInclui .Or. lGrava
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Tratamento para campos tipo Data em que o novo conteudo foi passado na forma de string ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			xAtual	:= (cAlias)->( FieldGet(nPosCpo) )
			If ValType(xAtual) == 'D' .And. ;
				ValType(xConteudo) == 'C'
				
	//			xAtual		:= DtoS(xAtual)
				xConteudo	:= StoD(xConteudo)
			EndIf
				
			//Compatibiliza com o tamanho da string e campos memo customizados
			If ValType(xConteudo) == 'C'
				nTamCpo		:= (cAlias)->( DBFieldInfo(nST_TAMANHO,nPosCpo) )
				cTipo			:= (cAlias)->( DBFieldInfo(nST_TIPO   ,nPosCpo) )
				If cTipo == 'C' //Tb pode ser um memo e neste caso nao mexe no tamanho
					xConteudo	:= PadR(xConteudo,nTamCpo)       
				EndIf
			EndIf
		EndIf
		
		//Se nao e' inclusao verifica se o conteudo novo e' diferente do gravado
		If !lInclui
			lGrava	:= .Not. ( xConteudo == xAtual )
		EndIf			
		
		//Grava apenas se o conteudo for diferente
		If lGrava
//			(cAlias)->( FieldPut(nPosCpo,xConteudo) )
			&(cAlias+'->'+cCampo)	:= xConteudo //Otimizacao, apos testes foi visto que assim e' muito mais rapido
			lAlterou	:= .T.
		EndIf

	EndIf
	
Return lAlterou