#INCLUDE "Protheus.ch"
#INCLUDE "RWMake.ch"
#INCLUDE "CXInclude.ch"

//ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
//±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
//±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
//±±³Programa  ³ CXFieldGet ³ Autor ³ Cirilo Rocha        ³ Data ³02/08/2011³±±
//±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
//±±³Descricao ³ Funcao customizada para simplificar o uso da fucao padrao  ³±±
//±±³          ³ FieldGet.                                                  ³±±
//±±³          ³                                                            ³±±
//±±³          ³ Ao invez de utilizar FieldGet(FieldPos(cCampo)) pode-se    ³±±
//±±³          ³ usar apenas U_CXFieldGet(cCampo)                           ³±±
//±±³          ³                                                            ³±±
//±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
//±±³   DATA   ³ Programador   ³ Manutencao efetuada                        ³±±
//±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
//±±³ 16/02/12 ³ Cirilo Rocha  ³ Ajuste para obter a tabela por parametro ou³±±
//±±³          ³               ³ com base no proprio campo visando simplifi-³±±
//±±³          ³               ³ car o uso desta funcao.                    ³±±
//±±³ 22/02/12 ³ Cirilo Rocha  ³ Acrescentado tratamento de erro para os ca-³±±
//±±³          ³               ³ sos do campo nao existir no dicionario.    ³±±
//±±³ 23/02/12 ³ Cirilo Rocha  ³ Feito tratamento para campos Memo virtuais ³±±
//±±³          ³               ³ (antigos)                                  ³±±
//±±³ 22/11/13 ³ Cirilo Rocha  ³ Feito tratamento para campos virtuais      ³±±
//±±³          ³               ³ Feito tratamento para campos que nao exis- ³±±
//±±³          ³               ³ tem.                                       ³±±
//±±³          ³               ³                                            ³±±
//±±³          ³               ³                                            ³±±
//±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
//±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
//ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
User Function CXFieldGet(cCampo,cAlias,lAliasCpo,lVirtual)

	Local nPos		:= 0
	Local xRetorno	:= NIL
	Local aArea		:= {}
	Local _INCLUI	:= .F.					//salva variavel publica INCLUI
	                                                   
	Default lAliasCpo	:= .F.
	Default lVirtual	:= .F.
	
	cCampo	:= U_CXRTrim(Upper(cCampo))
	
	If cAlias == NIL
		If lAliasCpo
			cAlias	:= U_CXTabela(cCampo)
		Else
			cAlias	:= Alias()
		EndIf
	EndIf
	
	nPos	:= (cAlias)->(FieldPos(cCampo))
	
	If nPos == 0
		If ValType('INCLUI') == 'L'
			_INCLUI	:= INCLUI
		EndIf
		INCLUI	:= .F. //seta para os campos Memo virtuais
	
		aArea		:= U_CXGetArea({'SX3'},.F.)
		SX3->(U_CXSetOrd(2)) // X3_CAMPO
		//Verifica se e' um Memo antigo (virtual)
		If SX3->(MsSeek(cCampo))
			If SX3->X3_CONTEXT == 'V' 
				If SX3->X3_TIPO == 'M' .Or. ;
					lVirtual
					
					dbSelectArea(cAlias)
		//			xRetorno	:= &(SX3->X3_RELACAO)
					xRetorno	:= CriaVar(cCampo,.T.)
				Else
					ApMsgAlert('CXFieldGet-001: O campo '+cCampo+' é virtual e não pode ser inicializado. Informe ao setor de T.I.')
				EndIf
			EndIf
		Else
			ApMsgAlert('CXFieldGet-002: O campo '+cCampo+' não foi localizado no SX3. Informe ao setor de T.I.')
		EndIf
		U_CXRestArea(aArea)
	
		INCLUI	:= _INCLUI
	Else
		xRetorno	:= (cAlias)->(FieldGet(nPos))
	EndIf
	
Return xRetorno