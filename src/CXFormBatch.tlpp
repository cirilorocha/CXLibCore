#INCLUDE 'RWMake.ch'
#INCLUDE 'Totvs.ch'
#INCLUDE 'ParmType.ch'
#INCLUDE 'CXInclude.ch'

/*=================================================================================================
Autor      : Cirilo Rocha
Data       : 22/03/2024 
Info       : Função baseada na função padrão FormBatch, acrescentando opção de usar texto customizado
				nos botões.
/*========+===============+======+=================================================================
   Data   | Responsável   |Ticket| Manutenção Efetuada
==========+===============+======+=================================================================
28/04/2025| Cirilo Rocha  |      | Modernização do visual da rotina
          |               |      | 
==========+===============+======+===============================================================*/
User Function CXFormBatch(	cTitle		AS Character,;	//01 cTitle
							aSays		AS Array	,;	//02 aSays
							aButtons	AS Array	,;	//03 aButtons
							bValid		AS CodeBlock,;	//04 bValid
							nAltura		AS Numeric	,;	//05 nAltura
							nLargura	AS Numeric	)	//06 nLargura

	//-- Declaração de Variáveis ----------------------------------------------
	Local aTxtBtn	:= {'Ok'		,;
						'Cancelar'	,;
						'Excluir'	,;
						'Incluir'	,;
						'Param.'	}				AS Array
	Local cCaption									AS Character
	Local nX										AS Numeric
	Local nTop										AS Numeric
	Local lEnabled									AS Logical
	Local nLarguraBox								AS Numeric
	Local nLarguraSay								AS Numeric
	Local oFont										AS Object
	Local oModal,oPnl								AS Object
	
	//-- Parâmetros da Rotina -------------------------------------------------
	ParamObg 0		VAR cTitle
	ParamObg 1		VAR aSays
	ParamObg 2		VAR aButtons

	Default nAltura		:= 200
	Default nLargura	:= 400

	//-- Inicializa Variáveis -------------------------------------------------
	oFont	:= TFont():New("Arial",,-14,,.F.)

    oModal  := FWDialogModal():New()
		oModal:SetEscClose(.T.)
		oModal:setTitle(cTitle)
		oModal:setSize(nAltura, nLargura)
		oModal:createDialog()
		IIf(bValid<>NIL,oModal:setValid(bValid),)

		oDlg := oModal:getPanelMain()
		oDlg:bEnd	:= {|| oModal:DeActivate() }

		oPnl := TPanel():New( ,,, oModal:getPanelMain() )
		oPnl:Align := CONTROL_ALIGN_ALLCLIENT

		nLarguraBox	:= (nLargura-10)

		TGroup():New( 10, 10, (nAltura-60), nLarguraBox,, oPnl,,,.T., )

		//-- Monta Says -------------------------------------------------------
		nTop := 20
		nLarguraSay	:= ( nLarguraBox-25 )
		For nX := 1 to Len(aSays)
			TSay():New(nTop,20,MontaBlock("{||'"+aSays[nX]+"'}"),oPnl,,oFont,.F.,.F.,.F.,.T.,,,nLarguraSay,10,.F.,.F.,.F.,.F.,.F.)
			nTop+= 10
		Next

		//-- Monta Botões -----------------------------------------------------
		For nX := 1 to Len(aButtons)
			If ValType(aButtons[nX][1]) == 'N'	//-- Compatibilidade com a função padrão!
				cCaption:= aTxtBtn[aButtons[nX][1]]
			Else
				cCaption:= aButtons[nX][1]
			EndIf			
			lEnabled	:= aButtons[nX][2]
			lEnabled	:= If(ValType(lEnabled) == "U",.T.,lEnabled)

			oModal:AddButton(cCaption,aButtons[nX][3],cCaption,,.T.,.F.,.T.,MontaBlock('{|| '+IIF(lEnabled,'.T.','.F.')+' }'))
		Next
	oModal:Activate()
	
Return
