#INCLUDE "Protheus.ch"
#INCLUDE "RWMake.ch"
#INCLUDE "Totvs.ch"
#Include "ParmType.ch"
#Include "CXInclude.ch"

//#############################################################################
//##+----------+----------+-------+---------------------+------+------------+##
//##|Programa  | CXLerLog | Autor | Cirilo Rocha        | Data | 30/11/2010 |##
//##+----------+----------+-------+---------------------+------+------------+##
//##|Descr.    | Função para descriptografar o conteúdo dos campos de audi- |##
//##|          |  toria do sistema (XX_USERLGI e XX_USERLGA)                |##
//##|          | Pode ser utilizado em campos virtual para exibir a infor-  |##
//##|          |  mação da auditoria. Ex.: U_CXLerLog(SE5->E5_USERLGI)      |##
//##|          |                                                            |##
//##|          | Baseado no padrão FwLeUsrLG (CFGX025.PRX)                  |##
//##+----------+------------------------------------------------------------+##
//##|Parametros| cConteudo = conteudo do campo de auditoria a ser exibido   |##
//##+----------+-----------+------------------------------------------------+##
//##|   DATA   |Programador| Manutenção efetuada                            |##
//##+----------+-----------+------------------------------------------------+##
//##| 14/03/19 | Cirilo R. | Pequena revisão no fonte                       |##
//##| 18/04/19 | Cirilo R. | Correção para apresentar o nome de usuário     |##
//##| 09/02/22 | Cirilo R. | Pequena revisão no fonte                       |##
//##| 23/05/23 | Cirilo R. | Correção para datar a partir de 19/05/2023     |##
//##|          |           |                                                |##
//##|          |           |                                                |##
//##+----------+-----------+------------------------------------------------+##
//#############################################################################
User Function CXLerLog(cConteudo);
							AS Character
	
	//Declaracao de variaveis----------------------------------------------------------------------
	Local cRet 		AS Character
	Local cUserLG   AS Character

	Local cUsr		AS Character
	Local cNmUsr	AS Character
	Local dData		AS Date

	//Parametros da rotina-------------------------------------------------------------------------
	ParamType 0		VAR cConteudo  	  		AS Character				Optional Default ''
	
	//Inicializa Variaveis-------------------------------------------------------------------------
	cRet := ""
	
	//---------------------------------------------------------------------------------------------
	If .Not. Empty(cConteudo)
		//LeLog(@cStatus,@cUsuarioI,@cUsuarioA,@dDataI,@dDataA,cTipo)
		//FWLeUserLg(cCampo, 2)
		
		cUserLG 	:= Embaralha(cConteudo,1)
		If .Not. Empty(cUserLg)
			If Subs(cUserLG, 1, 2) == "#@"
				cUsr 		:= AllTrim(Subs(cUserLG, 3, 6))
			Else
				cUsr	 	:= AllTrim(Subs(cUserLg, 3,12))
			EndIf

			cNmUsr		:= Upper(Left(U_CXNmUsr(cUsr),25))
			dData		:= CtoD("01/01/96","DDMMYY") + Load2in4(Substr(cUserLg,15))
			
			cRet 		:= DtoC(dData)+'-'+cUsr+"-"+cNmUsr
		EndIf
	EndIf
	
Return cRet 

//-------------------------------------------------------------------------------------------------
User Function LerLog(cConteudo)

Return U_CXLerLog(cConteudo)
