#INCLUDE "RWMake.ch"
#INCLUDE "Totvs.ch"
#INCLUDE "ParmType.ch"
#INCLUDE "PrConst.ch"
#INCLUDE "CXInclude.ch"

//#############################################################################
//##+----------+-----------+-------+-------------------+------+-------------+##
//##|Programa  | CXRotAuto | Autor | Cirilo Rocha      | Data | 26/03/2019  |##
//##+----------+-----------+-------+-------------------+------+-------------+##
//##|Descr.    | Função para detectar se a rotina esta atualmente no modo   |##
//##|          | execauto ou sem interface                                  |##
//##+----------+----------+-------------------------------------------------+##
//##| DATA     | ANALISTA | MANUTENCAO EFETUADA                             |##
//##+----------+----------+-------------------------------------------------+##
//##| 03/04/19 | Cirilo R.| Ajuste para considerar ou não rotinas automática|##
//##| 26/06/19 | Cirilo R.| Adicionadas novas rotinas automáticas           |##
//##| 17/11/21 | Cirilo R.| Pequena revisão no código                       |##
//##| 25/11/25 | Cirilo R.| Tratamento para execução automática via MVC     |##
//##|          |          |                                                 |##
//##+----------+----------+-------------------------------------------------+##
//#############################################################################
User Function CXRotAuto(lApenasJob	AS Logical	;	//01 lApenasJob
					,	oModelMVC	AS Object	;	//02 Objeto Modelo MVC (opcional)
					)								AS Logical
	
	//-- Declaração de Variáveis ----------------------------------------------
	Local lRet										AS Logical
	Local lRotAuto									AS Logical
	Local oViewActive								AS Object
	
	//-- Parâmetros da Rotina -------------------------------------------------
	ParamType 0		VAR lApenasJob			AS Logical		Optional Default .F.

	//-- Considera apenas os Jobs ---------------------------------------------
	If lApenasJob
		lRotAuto	:= .F.
	Else
		lRotAuto	:= 	FWIsInCallStack('FWMVCRotAuto') .Or. ;	//Execauto MVC
						FWIsInCallStack('MSExecAuto') 	.Or. ;	//Execauto padrao
						FWIsInCallStack('EnchAuto') 	.Or. ;	//Execauto enchoice
						FWIsInCallStack('mBrowseAuto')	.Or. ;	//Execauto browser
						( Type('ulCXRotAuto') == 'L' .And. ulCXRotAuto )
		
		//-- Rotina automática via MVC ---------------------------------------
		If 		.Not. lRotAuto	;
		.And.	oModelMVC <> NIL;

			//-- Vem de outro MVC, rotina automática (legado ou sem interface)
			oViewActive	:= FwViewActive()
			lRotAuto	:=	( oViewActive == NIL .And. .Not. FwIsInCallStack('OPENVIEW') )			;	//-- Sem view é automático de outra outra rotina, mas, na validação da activação, a View atual não está ativa
						.Or.( oViewActive <> NIL .And. .Not. ( oViewActive:oModel == oModelMVC ) )	;	//-- Vem de outra View MVC

		EndIf
	EndIf
	
	lRet	:= 	lRotAuto .Or. ;
				IsBlind() .Or. ;						//Sem Interface
				GetRemoteType() == NO_REMOTE .Or. ;		//Se SmartClient (-1)
				(Type('__cInterNet') == 'C' .And. __cInterNet == 'AUTOMATICO')	//Rotina automatica
	
Return lRet
