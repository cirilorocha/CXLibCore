#INCLUDE "Protheus.ch"
#INCLUDE "RWMake.ch"
#Include "ParmType.ch"
#INCLUDE "CXInclude.ch"

//
//ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
//ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
//ฑฑบPrograma  ณ CXSetUsr บ Autor ณ Cirilo Rocha       บ Data ณ  21/07/2016 บฑฑ
//ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
//ฑฑบDescrio ณ Funcao para execucao de testes, permitindo mudar de usuarioบฑฑ
//ฑฑบ          ณ durante o tempo de execucao                                บฑฑ
//ฑฑบ          ณ                                                            บฑฑ
//ฑฑฬออออออออออุออออออออออัอออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
//ฑฑบDATA      ณ ANALISTA ณ MOTIVO                                          บฑฑ
//ฑฑฬออออออออออุออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
//ฑฑบ 12/08/19 ณ Cirilo R.ณ Pequena revisao no fonte                        บฑฑ
//ฑฑบ 23/10/19 ณ Cirilo R.ณ Adicionado tratamento para execucao via fonte   บฑฑ
//ฑฑบ          ณ          ณ                                                 บฑฑ
//ฑฑบ          ณ          ณ                                                 บฑฑ
//ฑฑศออออออออออฯออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
//ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
User Function CXSetUsr(	cCodUsr	,;	//01
						lMsg	 )	//02
	
	//Declaracao de variaveis----------------------------------------------------------------------
	Local aParam	AS Array
	Local cPerg		AS Character
	Local cUsrPsw	AS Character
	Local nOrdem	AS Numeric

	//Parametros da rotina-------------------------------------------------------------------------
	ParamType 0		VAR cCodUsr			AS Character			Optional Default NIL
	ParamType 1		VAR lMsg			AS Logical				Optional Default .T.

	//Inicializa Variaveis-------------------------------------------------------------------------

	//Variaveis compartilhadas de mensagens de erro
	cMsgErr		:= IIf(Type('cMsgErr')<>'C','',cMsgErr)
	cCodErr		:= IIf(Type('cCodErr')<>'C','',cCodErr)
	cSoluc		:= IIf(Type('cSoluc')<>'C','',cSoluc)
	cMsg		:= IIf(Type('cMsg')<>'C','',cMsg)

	If cCodUsr == NIL
		aParam		:= U_CXGetParam(1) //Salva parametros
		cPerg		:= 'CXSETUSR'

		U_CXPergunta({	{"Usuแrio ?" 	,"C", len(__cUserID), 0,'NaoVazio() .And. UsrExist(MV_PAR01)','USR'}},cPerg)
		
		If .Not. Pergunte(cPerg,.T.)
			U_CXRestParam(aParam) //Restaura parametros
			Return .F.
		EndIf
		cCodUsr	:= MV_PAR01
		U_CXRestParam(aParam) 	//Restaura parametros
	EndIf
	
	//Salva dados atuais
	cUsrPsw	:= PswID()
	nOrdem	:= PswOrder(1)	//Ordem por codigo
	If PswSeek(cCodUsr)
		__cUserID	:= cCodUsr
		cUserName	:= UsrRetName(__cUserID)	//Atualizo o nome de usuario
	Else
		PswSeek(cUsrPsw)		//Restauro usuario PSW
		PswOrder(nOrdem)		//Restaura ordem
		
		cCodErr	:= U_CXTxtMsg(,,.T.)
		cMsgErr	:= 	'ERRO AO SETAR USUมRIO '+cCodUsr+CRLF+;
					'NรO FOI POSSอVEL LOCALIZAR O CำDIGO.'
		cSoluc	:= 'INFORME O SETOR DE T.I.'
		cMsg	:= cCodErr+': '+cMsgErr+IIf(!Empty(cSoluc),CRLF+'SOLUวรO: '+cSoluc,'')
		If lMsg
			U_CXHelp(cCodErr,,cMsgErr,,cSoluc)
		EndIf

		Return .F.
	EndIf

Return .T.