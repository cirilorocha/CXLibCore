#INCLUDE "Protheus.ch"
#INCLUDE "RWMake.ch"
#Include "ParmType.ch"
#Include "CXInclude.ch"

//#############################################################################
//##+==========+===========+=======+===================+======+=============+##
//##|Programa  | CXX3Combo | Autor | Cirilo Rocha      | Data | 03/03/2017  |##
//##+==========+===========+=======+===================+======+=============+##
//##|Descricao | Funcao que retorna a descricao do ComboBox                 |##
//##|          |                                                            |##
//##|          |                                                            |##
//##|          |                                                            |##
//##+==========+===============+============================================+##
//##|   DATA   | Programador   | Manutencao Efetuada                        |##
//##+==========+===============+============================================+##
//##| 30/01/19 | Cirilo Rocha  | Pequena revisao no fonte                   |##
//##|          |               |                                            |##
//##|          |               |                                            |##
//##+==========+===============+============================================+##
//#############################################################################
User Function CXX3Combo(cCampo		,;	//01 Campo do dicionario
						cAlias		,;	//02
						lAliasCpo	,;	//03
						lPrefix		,;	//04
						cConteudo	 )	//05
						
	//Declaracao de variaveis----------------------------------------------------------------------
	Local cRet		AS Character
	Local oArea		AS Object
	
	//Parametros da rotina-------------------------------------------------------------------------
	ParamType 0		VAR cCampo  	  		AS Character
	ParamType 1		VAR cAlias		  	  	AS Character				Optional Default NIL
	ParamType 2		VAR lAliasCpo  	  		AS Logical					Optional Default .T.
	ParamType 3		VAR lPrefix  	  		AS Logical					Optional Default .F.
	ParamType 4		VAR cConteudo  	  		AS Character				Optional Default NIL

	//Inicializa Variaveis-------------------------------------------------------------------------
	cCampo	:= Alltrim(cCampo)
	cRet	:= ''
	oArea	:= tCtrlAlias():GetArea('SX3')
	
	//---------------------------------------------------------------------------------------------
	If cAlias == NIL
		If lAliasCpo
			cAlias	:= U_CXTabela(cCampo) //Alias do campo
		Else
			cAlias	:= Alias()	//Alias corrente
		EndIf
	EndIf
	//Revalidacao da variavel
	ParamType 1		VAR cAlias		  	  	AS Character
	
	//---------------------------------------------------------------------------------------------
	If cConteudo == NIL
		If cAlias == 'M' //Memoria
			cConteudo	:= &('M->'+cCampo)
		Else
			cConteudo	:= U_CXFieldGet(cCampo,cAlias,/*lAliasCpo*/,/*lVirtual*/)
		EndIf
	EndIf
	//Revalidacao da variavel
	ParamType 4		VAR cConteudo  	  		AS Character
	
	//---------------------------------------------------------------------------------------------
	cRet		:= SX3->(X3Combo(cCampo,cConteudo))
	
	If lPrefix
		cRet	:= AllTrim(cConteudo)+' - '+cRet
	EndIf

	oArea:RestArea()		//Restaura area

Return cRet