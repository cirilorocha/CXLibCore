#INCLUDE 'RWMake.ch'
#INCLUDE 'Totvs.ch'
#INCLUDE 'ParmType.ch'
#INCLUDE 'CXInclude.ch'

//#############################################################################
//##+==========+=============+=======+==================+======+============+##
//##|Programa  | CXLibSetKey | Autor | Cirilo Rocha     | Data | 13/07/2017 |##
//##+==========+=============+=======+==================+======+============+##
//##|Descr.    | Biblioteca de funções para lidar com o comando SetKey      |##
//##+==========+===========+================================================+##
//##|   DATA   |Programador| Manutenção efetuada                            |##
//##+==========+===========+================================================+##
//##| 08/10/20 | Cirilo R. | Pequena revisão no fonte                       |##
//##|          |           |                                                |##
//##|          |           |                                                |##
//##+==========+===========+================================================+##
//#############################################################################


//#############################################################################
//##+==========+=============+=======+==================+======+============+##
//##|Programa  | CXGetKey    | Autor | Cirilo Rocha     | Data | 13/07/2017 |##
//##+==========+=============+=======+==================+======+============+##
//##|Descr.    | Função para armazenar e limpar as teclas de atalho SetKey  |##
//##+==========+===========+================================================+##
//##|   DATA   |Programador| Manutenção efetuada                            |##
//##+==========+===========+================================================+##
//##|          |           |                                                |##
//##|          |           |                                                |##
//##|          |           |                                                |##
//##+==========+===========+================================================+##
//#############################################################################
User Function CXGetKey(aTeclas);	//01
							AS Array
	
	//Declaracao de variaveis----------------------------------------------------------------------
	Local abSetKey		AS Array
	Local nX			AS Integer
	Local bTemp			AS CodeBlock

	//Parametros da rotina-------------------------------------------------------------------------
	ParamType 0		VAR aTeclas  		AS Array		Optional Default {VK_F4,VK_F5,VK_F6,VK_F7,VK_F8,VK_F9,VK_F10,VK_F11,VK_F12}
	
	//Inicializa Variaveis-------------------------------------------------------------------------
	abSetKey	:= {}
	
	For nX := 1 to len(aTeclas)
		bTemp	:= SetKey(aTeclas[nX],Nil)
		If bTemp <> Nil //Otimizacao
			aAdd(abSetKey,Array(2))
			aTail(abSetKey)[01]	:= aTeclas[nX]
			aTail(abSetKey)[02]	:= bTemp
		EndIf
	Next

Return abSetKey

//#############################################################################
//##+==========+=============+=======+==================+======+============+##
//##|Programa  | CXRestKey   | Autor | Cirilo Rocha     | Data | 13/07/2017 |##
//##+==========+=============+=======+==================+======+============+##
//##|Descr.    | Função para restaurar as teclas de atalho salvas pela roti-|##
//##|          | na CXGetKey                                                |##
//##+==========+===========+================================================+##
//##|   DATA   |Programador| Manutenção efetuada                            |##
//##+==========+===========+================================================+##
//##|          |           |                                                |##
//##|          |           |                                                |##
//##|          |           |                                                |##
//##+==========+===========+================================================+##
//#############################################################################
User Function CXRestKey(abSetKey)	//01
	
	//Declaracao de variaveis----------------------------------------------------------------------
	Local nX		AS Integer
	
	//Parametros da rotina-------------------------------------------------------------------------
	ParamType 0		VAR abSetKey  		AS Array

	//---------------------------------------------------------------------------------------------
	For nX := 1 to len(abSetKey)
		SetKey(abSetKey[nX][01],abSetKey[nX][02])
	Next

Return
